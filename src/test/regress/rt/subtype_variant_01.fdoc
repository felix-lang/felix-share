@h1 Subtyping: variants
@felix
typedef variants_t = (`Fred of int | `Joe of string | `Max);
var y : variants_t = (`Fred 43) :>> variants_t;
println$ y._strr;
var z  = ((#`Max) :>> variants_t);
println$ z._strr;

var a = case Joe "Hello";
println$ a._strr;

fun h(v:variants_t) => v._strr;

println$ h y;
println$ h z;
println$ h a;

fun k(v:variants_t) =>
  match v with
  | `Fred j => "Fred " + j.str
  | `Max => "Max"
  | _ => "some other variant"
;
println$ k y;
println$ k z;
println$ k a;

// simple case of covariant arguments
var cv = `June (a=1,b="Hello");
fun ck (v: (`June of (a:int))) => v._strr;
println$ ck cv;

@expect
`Fred (43)
#`Max
`Joe ('Hello')
`Fred (43)
#`Max
`Joe ('Hello')
Fred 43
Max
some other variant
`June ((a=1))
@
